plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'

    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'

    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services' // remote config
    id 'com.google.firebase.crashlytics' // crashlytics
    id 'com.google.firebase.firebase-perf' // performance
}

apply from: '../libraries.gradle'

def keystorePropertiesFile = rootProject.file(".gradle/signing.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "com.evangers.stockfield"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 4
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs { //keystore 정보
        release {
            storeFile new File(System.properties['user.home'], keystoreProperties['RELEASE_STORE_FILE'])
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'app'

    productFlavors {
        production {
            dimension 'app'
        }
        qa {
            dimension 'app'
            applicationIdSuffix '.test'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    setProperty("archivesBaseName", "v${defaultConfig.versionName}(${defaultConfig.versionCode})")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlin_version"
    implementation 'com.google.android.material:material:1.4.0'

    implementation androidxDependencies.values()
    implementation networkDependencies.values()
    implementation hiltDependencies.values()
    implementation coroutinesDependencies.values()
    implementation chart.values()
    implementation glide.values()

    implementation 'com.google.android.gms:play-services-ads:20.4.0' // admob
    implementation project(':nativetemplates') // admob

    implementation platform('com.google.firebase:firebase-bom:28.4.1') // remote config
    implementation 'com.google.firebase:firebase-config-ktx'// remote config
    implementation 'com.google.firebase:firebase-analytics-ktx'// remote config
    implementation 'com.google.firebase:firebase-database-ktx' // firebase realtime database
    implementation 'com.google.firebase:firebase-appcheck-safetynet:16.0.0-beta02'// firebase realtime database
    implementation 'com.google.firebase:firebase-crashlytics-ktx' // crashlytics
    implementation 'com.google.firebase:firebase-perf-ktx' // performance


    kapt kaptList.values()
    annotationProcessor annotationList.values()

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}